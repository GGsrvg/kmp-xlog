// Kotlin DSL only supports syntax shortcut for plugin inside plugins block,
// but plugins block doesn't support `if` syntax, so we have to stay on groovy.
plugins {
  alias libs.plugins.kmp
}

def hostOs = System.getProperty("os.name")
def isMacOS = hostOs == "Mac OS X"
def isWindows = hostOs.startsWith("Windows")
def isLinux = hostOs == "Linux"

if (isMacOS) {
  apply plugin: "com.android.library"
  apply plugin: "org.jetbrains.kotlin.native.cocoapods"
}

kotlin {
  if (isMacOS) {
    androidTarget()

    iosX64()
    iosArm64()
    iosSimulatorArm64()
    macosX64() {
      binaries {
        executable("kmp_xlog") {
          entryPoint = "com.piasy.kmp.xlog.example.main"
          linkerOpts = [
                  "-F${rootProject.projectDir}/kmp-xlog/build/cocoapods/publish/release/kmp_xlog.xcframework/macos-arm64_x86_64".toString(),
                  "-framework", "kmp_xlog", "-lz",
          ]
        }
      }
    }

    cocoapods {
      summary = "Some description for the Shared Module"
      homepage = "Link to the Shared Module homepage"
      version = Consts.releaseVersion
      ios.deploymentTarget = libs.versions.iosDeploymentTarget.get()
      podfile = project.file("../iosApp/Podfile")
      framework {
        baseName = "shared"
        isStatic = true
      }
    }

    js(IR) {
      browser {
      }
      binaries.executable()
    }
  }

  if (isLinux || isWindows) {
    def nativeTarget = isLinux ? linuxX64() : mingwX64()
    nativeTarget.binaries {
      executable("kmp_xlog") {
        entryPoint = "com.piasy.kmp.xlog.example.main"
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        //implementation("${Consts.releaseGroup}:${Consts.releaseName}:${Consts.releaseVersion}")
        implementation(project(":kmp-xlog"))
      }
    }

    if (isLinux || isWindows) {
      cppCommon {
        dependsOn(commonMain.get())
      }

      if (isLinux) {
        linuxX64Main {
          dependsOn(cppCommon.get())
        }
      }
      if (isWindows) {
        mingwX64Main {
          dependsOn(cppCommon.get())
        }
      }
    }
  }
}

if (isMacOS) {
  android {
    namespace = "${Consts.androidNS}.android"
    compileSdk = libs.versions.compileSdk.get().toInteger()
    defaultConfig {
      minSdk = libs.versions.minSdk.get().toInteger()
      targetSdk = libs.versions.targetSdk.get().toInteger()
    }

    compileOptions {
      sourceCompatibility JavaVersion.toVersion(libs.versions.jvm.get().toInteger())
      targetCompatibility JavaVersion.toVersion(libs.versions.jvm.get().toInteger())
    }

    kotlin {
      jvmToolchain libs.versions.jvm.get().toInteger()
    }
  }
}
